buildscript {
    ext.kotlin_version = '1.3.0'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

repositories {
    google()
    jcenter()
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "net.ebt.muzei.miyazaki"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 300
        versionName "3.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }

        def ghibliAuthorityValue = "net.ebt.muzei.miyazaki.ghibli"
        manifestPlaceholders = [ghibliAuthority: ghibliAuthorityValue]
        buildConfigField "String", "GHIBLI_AUTHORITY", "\"${ghibliAuthorityValue}\""
    }

    signingConfigs {
        release {
            Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            Properties keyProps = new Properties()
            if (localProps['keystore.props.file'] != null) {
                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            }
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.4.0"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.squareup.moshi:moshi:1.7.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'androidx.activity:activity:1.0.0-alpha01'
    implementation "androidx.room:room-runtime:2.1.0-alpha02"
    kapt "androidx.room:room-compiler:2.1.0-alpha02"
    implementation 'android.arch.work:work-runtime-ktx:1.0.0-alpha11'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.google.android.apps.muzei:muzei-api:3.1.0-alpha01'
}
