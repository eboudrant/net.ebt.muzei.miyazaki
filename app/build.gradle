buildscript {
    ext.kotlin_version = '1.2.71'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    google()
    jcenter()
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "net.ebt.muzei.miyazaki"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 123
        versionName "2.2"

        def ghibliAuthorityValue = "net.ebt.muzei.miyazaki.ghibli"
        manifestPlaceholders = [ghibliAuthority: ghibliAuthorityValue]
        buildConfigField "String", "GHIBLI_AUTHORITY", "\"${ghibliAuthorityValue}\""
    }

    sourceSets.main {
        java.srcDir 'src/main/java'
    }

    signingConfigs {
        release {
            Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            Properties keyProps = new Properties()
            if (localProps['keystore.props.file'] != null) {
                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            }
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }

        publicBeta.initWith(buildTypes.release)
        publicBeta {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
        }

        publicDebug.initWith(buildTypes.publicBeta)
        publicDebug {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.3.0"
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'androidx.core:core-ktx:0.3'
    implementation 'com.android.support:support-fragment:28.0.0'
    implementation 'android.arch.work:work-runtime-ktx:1.0.0-alpha10'
    implementation 'com.squareup.okhttp:okhttp:2.1.0'
    implementation 'com.google.android.apps.muzei:muzei-api:3.0.0'
}
